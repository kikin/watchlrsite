<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx" 
				xmlns:watchlr="com.watchlr.*" horizontalScrollPolicy="off" verticalScrollPolicy="off" 
				paddingTop="0" paddingBottom="0" paddingLeft="0" paddingRight="0" layout="absolute"
				minWidth="200" minHeight="200" applicationComplete="onApplicationComplete(event)"
				initialize="init(event)" xmlns:GigaPlayer="com.watchlr.GigaPlayer.*">
	<fx:Style source="assets/WatchlrPlayer.css"/>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import flash.external.*;
			
			import mx.core.FlexGlobals;
			import mx.events.FlexEvent;
			import mx.events.ResizeEvent;
			
			import org.osmf.events.LoaderEvent;
			
			private var _source:String;
			
			Security.allowInsecureDomain("*");
			Security.allowDomain("*");
			
			[bindable]
			public function set source(value:String):void{
				_source = value;
				gigaPlayer.source = _source;
			}
			
			private function init(event:FlexEvent):void{
				
				try {
					ExternalInterface.addCallback("play", gigaPlayer.play);
					ExternalInterface.addCallback("pause", gigaPlayer.pause);
					ExternalInterface.addCallback("seek", gigaPlayer.seek);
					ExternalInterface.addCallback("setSeekTarget", gigaPlayer.setSeekTarget);
					ExternalInterface.addCallback("getDuration", gigaPlayer.getDuration);
					ExternalInterface.addCallback("getCurrentTime", gigaPlayer.curTime);
					ExternalInterface.addCallback("enterFullScreen", enterFullScreen);
					ExternalInterface.addCallback("exitFullScreen", exitFullScreen);
					ExternalInterface.addCallback("setSource", setSource);
				} catch (error:SecurityError) {
					// Script access not enabled - Ignore!
				}
				
				gigaPlayer.width = width;
				gigaPlayer.height = height;
				
				addEventListener(Event.RESIZE, this.onResize);
				
				setChildIndex(gigaPlayer, 0);
				
			}
			
			private function onApplicationComplete(event:FlexEvent):void{
				stage.addEventListener(KeyboardEvent.KEY_DOWN, onKeyDown);
			}
			
			protected function setSource(src:String):void{
				source = src;
			}
			
			protected function onResize(resizeEvent:ResizeEvent):void{
				gigaPlayer.width = width;
				gigaPlayer.height = height;				
			}
			
			// toggle the display mode to full screen
			protected function enterFullScreen():void{
				FlexGlobals.topLevelApplication.stage.displayState = StageDisplayState.FULL_SCREEN;
			}
			
			protected function exitFullScreen():void{
					// set normal display
					stage.displayState = StageDisplayState.NORMAL;
			}
			
			protected function onKeyDown(event:KeyboardEvent):void{
				gigaPlayer.handleKeyPress(event);
			}
			
		]]>
	</fx:Script>
	
	<GigaPlayer:GigaPlayer id="gigaPlayer" width="100%" height="100%" x="0" y="0">
	</GigaPlayer:GigaPlayer>
	<!--<mx:ProgressBar id="seekProgress" x="0" y="377" width="500" height="50"/>-->
</mx:Application>
